plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id "io.kotest"
    id 'com.google.dagger.hilt.android'
    id 'androidx.room'
}

android {
    namespace 'org.wspcgir.strong_giraffe'
    compileSdk 35

    defaultConfig {
        applicationId "org.wspcgir.strong_giraffe"
        minSdk 33
        targetSdk 34
        versionCode 18
        versionName "0.9.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

    // Needed for KSP
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.13'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

room {
    schemaDirectory "$projectDir/schemas"
}

dependencies {

    // UI Framework
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.activity:activity-compose:1.10.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.3.1'
    implementation 'androidx.compose.foundation:foundation-layout-android:1.7.8'

    // Database
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'com.google.devtools.ksp:symbol-processing-api:1.7.0-1.0.6'
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.51.1'
    ksp 'com.google.dagger:hilt-compiler:2.51.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    // Navigation
    def compose_destinations_version = '1.6.38-beta'
    implementation "io.github.raamcosta.compose-destinations:core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    // Testing
    def kotest_version = '5.7.2'
    testImplementation "io.kotest:kotest-property-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.7.8"
    debugImplementation "androidx.compose.ui:ui-tooling:1.7.8"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.7.8"
}